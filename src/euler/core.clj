(ns euler.core
  (:use clojure.math.numeric-tower
        euler.math
        euler.combinators
        euler.combinatorics
        euler.sequences
        euler.strings
        euler.primes
        euler.lists))

(defn p1
  "If we list all the natural numbers below 10 that are multiples of 3 or 5, we
  get 3, 5, 6 and 9. The sum of these multiples is 23.

  Find the sum of all the multiples of 3 or 5 below 1000."
  []
  (sum (take-while (partial > 1000)
                   (filter (or* (partial divisible? 3)
                                (partial divisible? 5))
                           N))))

(defn p2
  "Each new term in the Fibonacci sequence is generated by adding the previous
  two terms. By starting with 1 and 2, the first 10 terms will be:

  1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

  By considering the terms in the Fibonacci sequence whose values do not exceed
  four million, find the sum of the even-valued terms."
  []
  (sum (filter even? (take-while (partial > 4000000) (fibonacci)))))

(defn p3
  "The prime factors of 13195 are 5, 7, 13 and 29.

  What is the largest prime factor of the number 600851475143 ?"
  []
  (apply max (prime-factorization 600851475143)))

(defn p4
  "A palindromic number reads the same both ways. The largest palindrome made
  from the product of two 2-digit numbers is 9009 = 91 x 99.

  Find the largest palindrome made from the product of two 3-digit numbers.OB"
  []
  ; I know a closed-form expression for this in the two-product case, but
  ; a solution general in the number of products turned out to be more
  ; interesting.
  (apply max
         (filter palindrome?
                 (map (partial apply *)
                      (handshake-product (range 999 100 -1) 2)))))

(defn p5
  "2520 is the smallest number that can be divided by each of the numbers from
  1 to 10 without any remainder.

  What is the smallest positive number that is evenly divisible by all of the
  numbers from 1 to 20?"
  []
  ; Call this number x. Let x's prime factorization be the list F. Since prime
  ; factorization is bijective, finding F is sufficient. For each factor f in
  ; 2, 3, 4, ... 20, f's prime factorization must be included in F. Hence F is
  ; the minimum list intersection of the prime factorizations of 2, 3, ... 20.
  (apply * (apply list-intersection (map prime-factorization (range 1 20)))))


(defn p6
  "The sum of the squares of the first ten natural numbers is,

  1^2 + 2^2 + ... + 10^2 = 385 The square of the sum of the first ten natural
  numbers is,

  (1 + 2 + ... + 10)^2 = 55^2 = 3025

  Hence the difference between the sum of the squares of the first ten natural
  numbers and the square of the sum is 3025 - 385 = 2640.

  Find the difference between the sum of the squares of the first one hundred
  natural numbers and the square of the sum."
  []
  (abs
    (- (sum (map square (take 100 ordinals)))
       (square (sum (take 100 ordinals))))))

(defn p7
  "By listing the first six prime numbers: 2, 3, 5, 7, 11, and 13, we can see
  that the 6th prime is 13.

  What is the 10 001st prime number?"
  []
  (nth primes 10000))
